load("@aio_npm//@angular-devkit/architect-cli:index.bzl", "architect", "architect_test")
load("@build_bazel_rules_nodejs//:index.bzl", "npm_package_bin")
load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")

# The write_source_files macro is used to write bazel outputs to the source tree and test that they are up to date.
# See: https://docs.aspect.build/aspect-build/bazel-lib/v0.5.0/docs/docs/write_source_files-docgen.html
load("@aspect_bazel_lib//lib:write_source_files.bzl", generated_files_test = "write_source_files")

exports_files([
    "firebase.json",
    "ngsw-config.template.json",
])

# Generate ngsw-config
npm_package_bin(
    name = "ngsw-config",
    outs = ["ngsw-config_generated.json"],
    args = ["$@"],
    tool = "//aio/scripts:build-ngsw-config",
)

# Write ngsw-config to the source directory and test that it's up to date
generated_files_test(
    name = "write-ngsw-config",
    files = {
        "ngsw-config.json": ":ngsw-config",
    },
)

# External deps required by dgeni transforms
DGENI_DEPS = [
    "@aio_npm//css-selector-parser",
    "@aio_npm//dgeni-packages",
    "@aio_npm//entities",
    "@aio_npm//fs-extra",
    "@aio_npm//hast-util-is-element",
    "@aio_npm//hast-util-to-string",
    "@aio_npm//ignore",
    "@aio_npm//image-size",
    "@aio_npm//json5",
    "@aio_npm//rehype-slug",
    "@aio_npm//remark",
    "@aio_npm//remark-html",
    "@aio_npm//rxjs",
    "@aio_npm//semver",
    "@aio_npm//stemmer",
    "@aio_npm//unist-util-filter",
    "@aio_npm//unist-util-source",
]

# All transforms and sources needed by dgeni to produce docs
DGENI_FILES = [
    "@angular_cli_src//:files_for_docgen",  # Downloaded cli snapshot (see WORKSPACE)
    "//aio/content",
    "//aio/content/examples",
    "//aio/src/assets",
    "//aio/tools/transforms",
    "//:package.json",
    "//packages:files_for_docgen",
]

# Run dgeni generation
npm_package_bin(
    name = "dgeni",
    args = ["./aio/tools/transforms/angular.io-package"],
    configuration_env_vars = ["PATH"],  # Need PATH for dgeni package that invokes git
    data = DGENI_FILES + DGENI_DEPS,
    env = {
        "BAZEL_DGENI_OUTPUT_PATH": "$(@D)",
    },
    output_dir = True,
    tool = "@aio_npm//dgeni/bin:dgeni",
)

# Generate example boilerplate files
npm_package_bin(
    name = "example-boilerplate",
    args = ["add"],
    env = {
        "BAZEL_EXAMPLE_BOILERPLATE_OUTPUT_PATH": "$(@D)",
    },
    output_dir = True,
    tool = "//aio/tools/examples:example-boilerplate",
)

# Combine example source files with generated boilerplate files
copy_to_directory(
    name = "examples",
    # Prevent sorting so that boilerplate overwrites example sources
    # buildifier: do not sort
    srcs = [
        "//aio/content/examples",
        ":example-boilerplate",
    ],
    replace_prefixes = {
        "content/examples": "",
        "example-boilerplate": "",
    },
)

# Generate stackblitz live examples
npm_package_bin(
    name = "stackblitz",
    args = [
        "$(RULEDIR)/examples",
        "$(@D)",
    ],
    data = [":examples"],
    output_dir = True,
    tool = "//aio/tools/stackblitz-builder:generate-stackblitz",
)

# Generate example zips
npm_package_bin(
    name = "example-zips",
    args = [
        "$(RULEDIR)/examples",
        "$(@D)",
    ],
    data = [":examples"],
    output_dir = True,
    tool = "//aio/tools/example-zipper:generate-example-zips",
)

# All source and configuration files required to build the docs app
APPLICATION_FILES = [
    "angular.json",
    "ngsw-config.json",
    "package.json",
    "tsconfig.app.json",
    "tsconfig.json",
    "tsconfig.worker.json",
    "//aio/src/assets",
    "//aio/src/assets/js",
    ":dgeni",
    ":stackblitz",
    ":example-zips",
] + glob(
    ["src/**/*"],
    exclude = [
        "src/**/*.spec.ts",
        # Temporarily exclude generated sources produced by the non-bazel
        # build until the whole project is built by bazel and this directory
        # isn't needed.
        "src/generated/**/*",
    ],
)

# External dependencies from aio/package.json required to build the docs app.
APPLICATION_DEPS = [
    "@aio_npm//@angular-devkit/build-angular",
    "@aio_npm//@angular/animations",
    "@aio_npm//@angular/cdk",
    "@aio_npm//@angular/cli",
    "@aio_npm//@angular/common",
    "@aio_npm//@angular/compiler",
    "@aio_npm//@angular/core",
    "@aio_npm//@angular/elements",
    "@aio_npm//@angular/forms",
    "@aio_npm//@angular/material",
    "@aio_npm//@angular/platform-browser",
    "@aio_npm//@angular/platform-browser-dynamic",
    "@aio_npm//@angular/router",
    "@aio_npm//@angular/service-worker",
    "@aio_npm//@types/lunr",
    "@aio_npm//@types/trusted-types",
    "@aio_npm//lunr",
    "@aio_npm//rxjs",
    "@aio_npm//safevalues",
    "@aio_npm//tslib",
    "@aio_npm//zone.js",
]

# All sources, specs, and config files required to test the docs app
TEST_FILES = APPLICATION_FILES + [
    "karma.conf.js",
    "tsconfig.spec.json",
] + glob(
    ["src/**/*.spec.ts"],
)

# External dependencies from aio/package.json required to test the docs app
TEST_DEPS = APPLICATION_DEPS + [
    "@aio_npm//@types/jasmine",
    "@aio_npm//@types/node",
    "@aio_npm//assert",
    "@aio_npm//jasmine",
    "@aio_npm//jasmine-core",
    "@aio_npm//karma-chrome-launcher",
    "@aio_npm//karma-coverage",
    "@aio_npm//karma-jasmine",
    "@aio_npm//karma-jasmine-html-reporter",
    "@aio_npm//puppeteer",
]

architect(
    name = "build",
    args = [
        "site:build:stable",
        "--outputPath=../$(@D)",
    ],
    chdir = package_name(),
    configuration_env_vars = ["NG_BUILD_CACHE"],
    data = APPLICATION_FILES + APPLICATION_DEPS,
    output_dir = True,
)

architect_test(
    name = "test",
    args = [
        "site:test",
        "--no-watch",
    ],
    chdir = package_name(),
    configuration_env_vars = ["NG_BUILD_CACHE"],
    data = TEST_FILES + TEST_DEPS,
)

architect(
    name = "serve",
    args = [
        "site:serve",
    ],
    chdir = package_name(),
    data = APPLICATION_DEPS + APPLICATION_FILES,
    tags = ["ibazel_notify_changes"],
)
